apply plugin: 'com.android.application'

def buildType

android {
    signingConfigs {
        config {
            storeFile file("../key.jks")
            storePassword "Samsung"
            keyAlias "key0"
            keyPassword "Samsung"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId = 'com.sec.dali_demo'
        minSdkVersion 26
        targetSdkVersion 26
        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions -frtti -w -Wall -std=gnu++11"
                arguments '-DANDROID_STL=c++_shared'
            }
        }
    }
    project.archivesBaseName = 'dali-demo'
    aaptOptions {
        noCompress ''
    }
    buildTypes {
        debug {
            ndk {
                abiFilters "armeabi-v7a"
            }
            buildType = "debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            ndk {
                abiFilters "armeabi-v7a"
            }

            signingConfig signingConfigs.config
            buildType = "release"
        }
    }
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
    }
    sourceSets {
        main {
            jniLibs.srcDirs += '../../../../dali-env/android/lib'
            assets.srcDirs += '../../../../dali-env/android/files'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    externalNativeBuild {
        cmake {
            version '3.10.2'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    buildToolsVersion = '28.0.3'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

task buildDaliDependencies(type:Exec) {
    def daliDir = file('../../../../dali-env/android').getAbsolutePath()
    environment "PREFIX", daliDir
    environment "ANDROID_ABI", "armeabi-v7a"
    environment "ANDROID_PLATFORM", "26"
    if (buildType == 'debug')
        environment "DEBUG", "1"
    workingDir "../../../../android-dependencies/cmake"
    commandLine 'sh',  './buildall.sh'
}

task buildDali(type:Exec) {
    workingDir "../dali"
    if (buildType == 'debug')
        environment "DEBUG", "1"
    commandLine 'sh', './build.sh'
}

task cleanDaliEnv(type:Delete) {
    def daliDir = file('../../../../dali-env/android').getAbsolutePath()
    delete daliDir
}

task cleanDaliDependencies(type:Exec) {
    workingDir "../../../../android-dependencies/cmake"
    commandLine 'sh',  './buildall.sh', 'clean'
}

task cleanDali(type:Exec) {
    workingDir "../dali"
    commandLine 'sh', './build.sh', 'clean'
}

buildDali.dependsOn buildDaliDependencies
preBuild.dependsOn buildDali

cleanDaliDependencies.dependsOn cleanDaliEnv
cleanDali.dependsOn cleanDaliDependencies
clean.dependsOn cleanDali


